plugins {
    id 'io.freefair.lombok' version '6.3.0'
    id 'kr.entree.spigradle' version '2.4.3'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

archivesBaseName = 'monad-uhc'

repositories {
    mavenCentral()
    jitpack()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

ext {
    versions = [
        paper: '1.19.4',
        cloudPaper: '1.8.3',
        adventureTextMinimessage: '4.13.0'
    ]
}

dependencies {
    /** Import PaperMC */
    compileOnly paper(versions.paper)
    /**Import incendo/cloud*/
    implementation "cloud.commandframework:cloud-core:${versions.cloudPaper}"
    implementation "cloud.commandframework:cloud-annotations:${versions.cloudPaper}"
    implementation "cloud.commandframework:cloud-minecraft-extras:${versions.cloudPaper}"
    annotationProcessor "cloud.commandframework:cloud-annotations:${versions.cloudPaper}"
    implementation "cloud.commandframework:cloud-brigadier:${versions.cloudPaper}"
    implementation "cloud.commandframework:cloud-paper:${versions.cloudPaper}"
    /** Import Minimessage */
    implementation "net.kyori:adventure-text-minimessage:${versions.adventureTextMinimessage}"
    /** Import reflections */
    implementation 'org.reflections:reflections:0.10.2'
    /** Import Mockbukkit */

    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.19:2.144.3'
    testImplementation 'org.assertj:assertj-core:3.24.1'
    testImplementation 'com.google.code.gson:gson:2.10.1'
}

def branch() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def date() {
    def date = new Date()
    def formattedDate = date.format('yyyy.M.d')
    return formattedDate
}

def hash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    stdout.toString().trim()
}

spigot {
    authors 'jcedeno'
    apiVersion '1.19'
    version "${branch()}-${date()} (${hash()})"
    name 'monad-uhc'
    load STARTUP
}

shadowJar {
    relocate 'fr.mrmicky', 'us.jcedeno.anmelden.shadded.fr.mrmicky'
}

build.dependsOn shadowJar
